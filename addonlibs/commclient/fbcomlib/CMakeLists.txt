CMAKE_MINIMUM_REQUIRED(VERSION 3.11)

SET(LIB_NAME fbcomlib)

SET(KSAPI_DIR ${CMAKE_CURRENT_LIST_DIR}/../ksapi)
SET(KSAPI_MODEL_DIR ${KSAPI_DIR}/model)
SET(KSAPI_SRC_DIR ${KSAPI_DIR}/source)
SET(KSAPI_INC_DIR ${KSAPI_DIR}/include)

SET(KSAPI_GEN_SRC ksapi.c ksapi.h)
SET(KSAPI_MODEL_FILE ${KSAPI_MODEL_DIR}/ksapi.ovm)

################ GENERIC PART #################################
SET(OV_SRC ${CMAKE_CURRENT_BINARY_DIR}/ov.h)
SET(FB_SRC ${CMAKE_CURRENT_BINARY_DIR}/fb.h)
SET(KSBASE_SRC ${CMAKE_CURRENT_BINARY_DIR}/ksbase.h)

SET(LIB_ROOT ${CMAKE_CURRENT_LIST_DIR})
SET(LIB_INC_DIR ${LIB_ROOT}/include)
SET(LIB_SRC_DIR ${LIB_ROOT}/source)
SET(LIB_MODEL_DIR ${LIB_ROOT}/model)

file(GLOB LIB_SRC "${LIB_SRC_DIR}/*.c")

SET(LIB_GEN_SRC ${LIB_NAME}.c ${LIB_NAME}.h)
SET(LIB_MODEL_FILE ${LIB_MODEL_DIR}/${LIB_NAME}.ovm)

INCLUDE_DIRECTORIES(${OV_INCLUDE_DIR} ${OV_MODEL_DIR} ${KSBASE_INCLUDE_DIR} ${KSBASE_MODEL_DIR} ${LIB_INC_DIR} ${LIB_MODEL_DIR} ${FB_INCLUDE_DIR} ${FB_MODEL_DIR} ${KSAPI_INC_DIR} ${KSAPI_MODEL_DIR} ${CMAKE_CURRENT_BINARY_DIR})

SET(GENERIC_LIB_DEFINES -DOV_DEBUG -DOV_COMPILE_LIBRARY_${LIB_NAME})
SET(LIB_DEFINES ${SPECIAL_LIB_DEFINES} ${GENERIC_LIB_DEFINES})

############### Generate model files ###############################
ADD_CUSTOM_COMMAND(OUTPUT ${OV_SRC}
	COMMAND $<TARGET_FILE:ov_codegen> -I ${OV_MODEL_DIR} -f ${OV_MODEL_FILE} -o ${CMAKE_CURRENT_BINARY_DIR} -l ov
    DEPENDS ${OV_MODEL_FILE})
    
ADD_CUSTOM_COMMAND(OUTPUT ${KSBASE_SRC}
    COMMAND $<TARGET_FILE:ov_codegen> -I ${OV_MODEL_DIR} -f ${KSBASE_MODEL_FILE} -o ${CMAKE_CURRENT_BINARY_DIR} -l ksbase
	DEPENDS ${KSBASE_MODEL_FILE})

ADD_CUSTOM_COMMAND(OUTPUT ${FB_SRC}
    COMMAND $<TARGET_FILE:ov_codegen> -I ${OV_MODEL_DIR} -I ${KSBASE_MODEL_DIR} -f ${FB_MODEL_FILE} -o ${CMAKE_CURRENT_BINARY_DIR} -l fb
	DEPENDS ${FB_MODEL_FILE})

ADD_CUSTOM_COMMAND(OUTPUT ${KSAPI_GEN_SRC}
	COMMAND $<TARGET_FILE:ov_codegen> -I ${OV_MODEL_DIR} -I ${KSBASE_MODEL_DIR} -f ${KSAPI_MODEL_FILE} -o ${CMAKE_CURRENT_BINARY_DIR} -l ksapi
	DEPENDS ${KSAPI_MODEL_FILE})

ADD_CUSTOM_COMMAND(OUTPUT ${LIB_GEN_SRC}
	COMMAND $<TARGET_FILE:ov_codegen> -I ${OV_MODEL_DIR} -I ${KSBASE_MODEL_DIR} -I ${FB_MODEL_DIR} -I ${KSAPI_MODEL_DIR} -f ${LIB_MODEL_FILE} -o ${CMAKE_CURRENT_BINARY_DIR} -l ${LIB_NAME}
    DEPENDS ${LIB_MODEL_FILE})
    
################ Generate the library ################################
ADD_LIBRARY(${LIB_NAME} SHARED ${LIB_SRC} ${OV_SRC} ${KSBASE_SRC} ${FB_SRC} ${LIB_GEN_SRC} ${OV_MODEL_FILE} ${LIB_MODEL_FILE} ${KSAPI_GEN_SRC})
TARGET_LINK_LIBRARIES(${LIB_NAME} libov fb ksapi ksbase)
IF(${MSVC})
	TARGET_LINK_LIBRARIES(${LIB_NAME} ws2_32)
ENDIF()
TARGET_COMPILE_DEFINITIONS(${LIB_NAME} PRIVATE ${OV_DEFINES} ${LIB_DEFINES})
SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES PREFIX "")

# Has no purpose for the build but adds the modelling files to the solution
SET(MODEL_FILES		${LIB_MODEL_DIR}/${LIB_NAME}.ovt
					${LIB_MODEL_DIR}/${LIB_NAME}.ovm
					${LIB_MODEL_DIR}/${LIB_NAME}.ovf)
ADD_CUSTOM_TARGET(${LIB_NAME}_Model SOURCES ${MODEL_FILES})

###################### Installation part for addon library ########################

# Install the DLL into the addonlibs directory
INSTALL(TARGETS ${LIB_NAME}
	RUNTIME DESTINATION ${ADDONLIB_INSTALL_DIR}
	LIBRARY DESTINATION ${ADDONLIB_INSTALL_DIR})